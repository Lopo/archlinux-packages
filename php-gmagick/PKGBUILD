# Contributor: Pavol Hluchy (Lopo) <lopo@losys.eu>

pkgname=php-gmagick
pkgver=1.1.7RC2
pkgrel=1
pkgdesc="PHP extension for GMagick"
license=('PHP')
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/gmagick"
depends=(
	'php>=5.1.3'
	'graphicsmagick>=1.2.6'
	)
backup=('etc/php/conf.d/gmagick.ini')
install=php-gmagick.install
source=("http://pecl.php.net/get/gmagick-$pkgver.tgz")
sha512sums=('13d8f26379fc0649e0056f51f4911c744439a6957786451a93e5abd6112015bc4941fffa534308c03fc954d3defd433250e7fec4454a3409ab340a3d21c324ce')

build() {
	cd "$srcdir/gmagick-$pkgver"

	phpize || return 1
	./configure --prefix=/usr
	make || return 1
	}

package() {
	cd "$srcdir/gmagick-$pkgver"

	make INSTALL_ROOT="$pkgdir" install
	echo ';extension=gmagick.so' > gmagick.ini
	install -D -m644 gmagick.ini "$pkgdir/etc/php/conf.d/gmagick.ini"
	}
