# Maintainer: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>

pkgname=gitlab-shell
pkgver=2.0.1
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab-shell/config.yml')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v${pkgver}.tar.gz")
sha512sums=('381b71001526530d6fdba02abefbd812d9b1ad338bedc8599ce632bde20665a9c07df22c81634a505d8b6e71cf5b5824fa6f1219ac4789c05d3a99e52d972c24')
_homedir=/var/lib/gitlab
_datadir=/usr/share/webapps/gitlab-shell
_srcdir="gitlab-shell-${pkgver}"

package() {
	install -d \
		"${pkgdir}${_datadir}" \
		"${pkgdir}${_homedir}" \
		"${pkgdir}/etc/webapps/gitlab-shell"

	cd "${srcdir}/${_srcdir}"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|${_homedir}/repositories|" \
		-e "s|/home/git|${_homedir}|" \
		config.yml.example > "${pkgdir}/etc/webapps/gitlab-shell/config.yml"
	ln -fs /etc/webapps/gitlab-shell/config.yml "${pkgdir}${_datadir}/config.yml"

	cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
	ln -fs ${_datadir} ${pkgdir}/${_homedir}/

	install -dm700 "${pkgdir}${_homedir}/.ssh"
	install -dm770 "${pkgdir}${_homedir}/"{repositories,satellites}

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=4 sw=4 et:
