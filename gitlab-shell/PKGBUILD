# Maintainer: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>

pkgname=gitlab-shell
pkgver=2.0.0
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab-shell/config.yml')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v${pkgver}.tar.gz")
sha512sums=('87467b12b5e41f8265e9eb1ab361b80d1133887925a8555820902d7c0cc9e65ce5342de283b50c4104796be50db4fc9338cdbc2c5d340614dfd7c961c95a280d')
_homedir=/var/lib/gitlab
_datadir=/usr/share/webapps/gitlab-shell
_srcdir="gitlab-shell-${pkgver}"

package() {
	install -d \
		"${pkgdir}${_datadir}" \
		"${pkgdir}${_homedir}" \
		"${pkgdir}/etc/webapps/gitlab-shell"

	cd "${srcdir}/${_srcdir}"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|${_homedir}/repositories|" \
		-e "s|/home/git|${_homedir}|" \
		config.yml.example > "${pkgdir}/etc/webapps/gitlab-shell/config.yml"
	ln -fs /etc/webapps/gitlab-shell/config.yml "${pkgdir}${_datadir}/config.yml"

	cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
	ln -fs ${_datadir} ${pkgdir}/${_homedir}/

	install -dm700 "${pkgdir}${_homedir}/.ssh"
	install -dm770 "${pkgdir}${_homedir}/"{repositories,satellites}

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=4 sw=4 et:
