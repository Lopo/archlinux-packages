# Maintainer: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-shell
pkgver=1.9.3
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab/shell.yml')
#source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitlab-shell/repository/archive.tar.gz?ref=v${pkgver}")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v${pkgver}.tar.gz")
sha512sums=('4d14e731c865528650b134e493ce617b24971a927931c648253f9c3614d72eb21533f63984cb7cfc38a0201e3dfec3c2f5f57d6bd29fe791689548eecd8e9c3b')
_homedir=/var/lib/gitlab
_datadir=/usr/share/gitlab-shell
_srcdir="gitlab-shell-${pkgver}"

package() {
	install -d \
		"${pkgdir}${_datadir}" \
		"${pkgdir}${_homedir}" \
		"${pkgdir}/etc/webapps/gitlab"

	cd "${srcdir}/${_srcdir}"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|${_homedir}/repositories|" \
		-e "s|/home/git|${_homedir}|" \
		config.yml.example > "${pkgdir}/etc/webapps/gitlab/shell.yml"
	ln -fs /etc/webapps/gitlab/shell.yml "${pkgdir}${_datadir}/config.yml"

	cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
	ln -fs /usr/share/gitlab-shell ${pkgdir}/${_homedir}/

	install -dm700 "${pkgdir}${_homedir}/.ssh"
	install -dm770 "${pkgdir}${_homedir}/"{repositories,satellites}

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
