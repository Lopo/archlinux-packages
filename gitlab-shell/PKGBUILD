# Maintainer: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-shell
pkgver=1.9.1
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab/shell.yml')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitlab-shell/repository/archive.tar.gz?ref=v${pkgver}")
sha512sums=('2b20be959e803e0483302ba6b4e68cd53304167f9ceece2da02c0c16ad0d886df2d64423309217291300032293ef3f9cf4592b72c4622804463c74183ba0614d')
_homedir=/var/lib/gitlab
_datadir=/usr/share/gitlab-shell

package() {
	install -d \
		"${pkgdir}${_datadir}" \
		"${pkgdir}${_homedir}" \
		"${pkgdir}/etc/webapps/gitlab"

	cd "${srcdir}/${pkgname}.git"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|${_homedir}/repositories|" \
		-e "s|/home/git|${_homedir}|" \
		config.yml.example > "${pkgdir}/etc/webapps/gitlab/shell.yml"
	ln -fs /etc/webapps/gitlab/shell.yml "${pkgdir}${datadir}/config.yml"
	
	cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
	ln -fs /usr/share/gitlab-shell ${pkgdir}/${_homedir}/
	
	install -dm700 "${pkgdir}${_homedir}/.ssh"
	install -dm770 "${pkgdir}${_homedir}/"{repositories,satellites}
	
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
